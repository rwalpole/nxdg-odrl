BASE <http://cat.nationalarchives.gov.uk/>

PREFIX cat: <http://cat.nationalarchives.gov.uk/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nat: <http://www.nationalarchives.gov.uk/>
PREFIX odrl: <http://www.w3.org/ns/odrl/2/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
          ?policyUri a odrl:Policy, odrl:Offer ;
                     odrl:profile nat:odrl-profile ;
                     odrl:conflict ?conflict ;
                     odrl:permission ?b1 ;
                     odrl:prohibition ?b2 .
          ?b1 ?p1 ?v1 ;
              odrl:assigner cat:The_National_Archives ;
              odrl:target ?resource .
          ?b2 ?p2 ?v2 ;
              odrl:assigner cat:The_National_Archives ;
              odrl:target ?resource .
          ?b1 odrl:constraint ?c1 .
          ?c1 ?c2 ?c3 .
          ?b1 odrl:constraint ?bnode .
          ?bnode rdf:type odrl:Constraint ;
                 odrl:leftOperand odrl:dateTime ;
                 odrl:operator odrl:gt ;
                 odrl:rightOperand ?openingDate .
          }
WHERE
{
    BIND(cat:ADM.2021.2NW6VY.P.1 AS ?resource)
    ?resource dct:identifier ?identifier
    BIND(URI(CONCAT(?identifier,"-policy")) AS ?policyUri)
    {
        {
            SELECT DISTINCT ?permission
            WHERE {
                      BIND(cat:ADM.2021.2NW6VY.P.1 AS ?resource)
                      ?resource dct:accessRights ?accessRights .
                      ?accessRights odrl:hasPolicy ?policy .
                      { ?policy odrl:permission ?permission . }
                      UNION
                      {
                          ?policy odrl:inheritFrom+ ?parentPolicy .
                          ?parentPolicy odrl:permission ?permission
                      }
                  }
            }
        BIND(BNODE() AS ?b1)
        OPTIONAL {
                     ?permission odrl:constraint ?constraint .
                     FILTER NOT EXISTS { ?constraint odrl:andSequence ?sequence . }
                     BIND(BNODE() AS ?c1)
                     ?constraint ?c2 ?c3 .
                 }
        OPTIONAL {
                     ?permission odrl:constraint ?constraint .
                     ?constraint odrl:andSequence ?seq .
                     ?seq rdf:first ?firstConstraint .
                     ?seq rdf:rest/rdf:first ?secondConstraint .
                     ?firstConstraint odrl:rightOperand ?lastDate .
                     ?secondConstraint  odrl:rightOperand ?duration .
                     BIND((?lastDate + ?duration) AS ?openingDate) .
                     BIND(BNODE() AS ?bnode)
                 }
        ?permission ?p1 ?v1 .
        FILTER(?p1 NOT IN(odrl:constraint)) .
    }
    UNION
    {
        {
            SELECT DISTINCT ?prohibition
            WHERE {
                      BIND(cat:ADM.2021.2NW6VY.P.1 AS ?resource)
                      ?resource dct:accessRights ?accessRights .
                      ?accessRights odrl:hasPolicy ?policy .
                      { ?policy odrl:prohibition ?prohibition . }
                      UNION
                      {
                          ?policy odrl:inheritFrom+ ?parentPolicy .
                          ?parentPolicy odrl:prohibition ?prohibition
                      }
                  }
            }
        BIND(BNODE() AS ?b2)
        ?prohibition ?p2 ?v2 .
    }
    {
        SELECT DISTINCT ?conflict
        WHERE {
                  BIND(cat:ADM.2021.2NW6VY.P.1 AS ?resource)
                  ?resource dct:accessRights ?accessRights .
                  ?accessRights odrl:hasPolicy ?policy .
                  { ?policy odrl:conflict ?conflict . }
                  UNION
                  {
                      ?policy odrl:inheritFrom+ ?parentPolicy .
                      ?parentPolicy odrl:conflict ?conflict .
                  }
              }
        }
}